buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.25.4'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
    maven { url "https://jitpack.io" }
}

android {
    compileSdkVersion 28

    dataBinding.enabled = true

    defaultConfig {
        applicationId "com.pluscubed.matlog"
        minSdkVersion 16
        targetSdkVersion 28
        versionName '1.1.2'
        versionCode 7

        testApplicationId "com.pluscubed.matlog.test"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        manifestPlaceholders = [appName: "@string/app_name"]

        vectorDrawables.useSupportLibrary = true
    }

    if (project.hasProperty("RELEASE_STORE_FILE")) {
        signingConfigs {
            release {
                storeFile file(RELEASE_STORE_FILE)
                storePassword RELEASE_STORE_PASSWORD
                keyAlias RELEASE_KEY_ALIAS_MATLOG
                keyPassword RELEASE_KEY_PASSWORD_MATLOG
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            if (project.hasProperty("RELEASE_STORE_FILE")) {
                signingConfig signingConfigs.release
            } else {
                signingConfig signingConfigs.debug
            }
        }
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '-DEBUG'
            manifestPlaceholders = [appName: "MatLog DEBUG"]
            ext.enableCrashlytics = false
        }
    }

    flavorDimensions "store"
    productFlavors {
        play {
            dimension "store"
        }
        fdroid {
            dimension "store"
            applicationId "com.pluscubed.matloglibre"
            manifestPlaceholders = [appName: "@string/app_name_libre"]
        }
    }
}

def supportLibrariesVersion = "28.0.0-rc02"

dependencies {
    implementation "com.android.support:appcompat-v7:$supportLibrariesVersion"
    implementation "com.android.support:recyclerview-v7:$supportLibrariesVersion"
    implementation "com.android.support:support-annotations:$supportLibrariesVersion"
    implementation "com.android.support:design:$supportLibrariesVersion"
    implementation "com.android.support:support-v13:$supportLibrariesVersion"

    implementation 'com.afollestad.material-dialogs:commons:0.9.4.5'

    implementation 'com.google.firebase:firebase-core:16.0.3'
    playImplementation('com.crashlytics.sdk.android:crashlytics:2.9.5@aar') {
        transitive = true;
    }

    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testImplementation 'junit:junit:4.12'
}